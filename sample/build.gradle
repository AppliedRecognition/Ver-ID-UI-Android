apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.appliedrec.verid.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.generateVersionCode()
        versionName rootProject.generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        multiDexEnabled true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += ["src/main/application"]
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/androidx.exifinterface_exifinterface.version'
    }
}

apply from: "../sample-dependencies.gradle"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    debugApi project(':veridui')
    debugImplementation "com.appliedrec.verid:core2:${rootProject.generateVersionName()}"
    releaseImplementation "com.appliedrec.verid:ui2:${rootProject.generateVersionName()}"
    for (dep in coreLibraryDesugaringDependencies) {
        coreLibraryDesugaring dep.toString()
    }
    for (dep in sampledependencies) {
        implementation dep.toString()
    }
    for (dep in sampleAndroidTestDependencies) {
        androidTestImplementation dep.toString()
    }
    implementation("com.appliedrec.verid:serialization:1.0.0") {
        transitive=false
    }
    implementation 'com.google.protobuf:protobuf-javalite:[3.8.0,4['
    androidTestUtil "androidx.test:orchestrator:${rootProject.androidTestVersion}"

    annotationProcessor "androidx.room:room-compiler:${rootProject.roomVersion}"
}
