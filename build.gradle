// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

class Dependency {
    String groupId
    String artifactId
    String version

    Dependency(groupId, artifactId, version) {
        this.groupId = groupId
        this.artifactId = artifactId
        this.version = version
    }

    String toString() {
        return "${groupId}:${artifactId}:${version}"
    }
}

ext {
    versionMajor = 2
    versionMinor = 2
    versionPatch = 1
    versionClassifier = null
    versionClassifierVersion = null

    minSdkVersion = 21
    compileSdkVersion = 30
    buildToolsVersion = "30.0.3"
    targetSdkVersion = 31

    junitVersion = '1.1.2'
    espressoVersion = '3.3.0'
    androidTestVersion = '1.3.0'
    roomVersion = '[2.2.0,3.0.0['
}

Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier + ".${ext.versionClassifierVersion}"
    }
    return versionName
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://dev.ver-id.com/artifactory/gradle-release'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
