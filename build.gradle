// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

class Dependency {
    String groupId
    String artifactId
    String version

    Dependency(groupId, artifactId, version) {
        this.groupId = groupId
        this.artifactId = artifactId
        this.version = version
    }

    String toString() {
        return "${groupId}:${artifactId}:${version}"
    }
}

ext {
    versionMajor = 2
    versionMinor = 0
    versionPatch = 0
    versionClassifier = "rc"
    versionClassifierVersion = "02"

    minSdkVersion = 21
    compileSdkVersion = 30
    buildToolsVersion = "30.0.2"
    targetSdkVersion = 30

    junitVersion = '1.1.2'
    espressoVersion = '3.3.0'
    androidTestVersion = '1.3.0'
    roomVersion = '[2.2.0,3.0.0['
}

Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier + ".${ext.versionClassifierVersion}"
    }
    return versionName
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://dev.ver-id.com/artifactory/gradle-release'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
