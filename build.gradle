// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

class Dependency {
    String groupId
    String artifactId
    String version

    Dependency(groupId, artifactId, version) {
        this.groupId = groupId
        this.artifactId = artifactId
        this.version = version
    }

    String toString() {
        return "${groupId}:${artifactId}:${version}"
    }
}

ext {
    versionMajor = 2
    versionMinor = 6
    versionPatch = 2
    versionClassifier = null
    versionClassifierVersion = null

    minSdkVersion = 21
    targetSdkVersion = 33
    buildToolsVersion = "30.0.3"
    compileSdkVersion = 33

    roomVersion = '[2.2.0,3.0.0['
    appcompatVersion = '[1.1.0,2.0.0['
    exifinterfaceVersion = '[1.1.0,2.0.0['
    annotationVersion = '[1.1.0,2.0,0['
    androidSDKVersion = '28.0.0'
    gsonVersion = '[2.8.5,3.0.0['
    constraintLayoutVersion = '[2.0,3.0['
    relinkerVersion = '[1.3.1,2.0.0['
    rxJavaVersion = '[3.0.4,4.0['
    rxAndroidVersion = '[3.0,4.0['
    preferenceVersion = '[1.1.1,2.0.0['
    veridIdentityVersion = '[1.1.1,2.0['
    espressoCoreVersion = '[3.2.0,4.0.0['
    lifecycleCommonJava8Version = '[2.2.0,3.0.0['
    multidexVersion = '[2.0.1,3.0.0['
    cameraxVersion = '1.0.0-beta06'
    cameraxViewVersion = '1.0.0-alpha13'
    desugarJdkLibVersion = '[1.0.9,2.0.0['
    cborVersion = '2.11.1'
    mlKitFaceDetectionVersion = '[16.0.1,17.0.0['
    junitVersion = '1.1.3'
    espressoVersion = '3.4.0'
    androidTestVersion = '1.4.0'
    guavaVersion = "30.1.1-android"
}

Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier + ".${ext.versionClassifierVersion}"
    }
    return versionName
}

allprojects {
    repositories {
        google()
        //noinspection JcenterRepositoryObsolete
        jcenter()
        mavenCentral()
        maven {
            url 'https://dev.ver-id.com/artifactory/gradle-release'
        }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
